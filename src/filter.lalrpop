
use ast;

use std::sync::Arc;
use std::path::PathBuf;
use ast::block_statements as stm;
use ast::{Node, AstLocation, Filter};
use ast::block::{Block, BlockType};
use tok::Location as TokenLocation;
use tok::Tok;
use ast::import::ImportStatement;
use ast::var::{VarDefinition,VarReference};
use ast::expression::{ExpressionValue, ExpressionNode, ExpressionOperation};

#[LALR]
grammar<'a>(file: &'a Arc<PathBuf>);

extern {
    type Location = TokenLocation;
    type Error = char;

    enum Tok {
        "(" => Tok::LParen,
        ")" => Tok::RParen,
        "-" => Tok::Minus,
        "+" => Tok::Plus,
        "*" => Tok::Times,
        "/" => Tok::Div,
        "," => Tok::Comma,
        "\n" => Tok::NewLine,
        "Show" => Tok::Show,
        "Hide" => Tok::Hide,
        "Mixin" => Tok::Mixin,
        "Import" => Tok::Import,
        ">=" => Tok::Gte,
        ">" => Tok::Gt,
        "<=" => Tok::Lte,
        "<" => Tok::Lt,
        "=" => Tok::Eql,
        "if" => Tok::If,
        "True" => Tok::True,
        "False" => Tok::False,
        Num => Tok::Num(<i64>),
        Float => Tok::Float(<f64>),
        QuotedStrLiteral => Tok::StrLiteral(<String>),
        Constant => Tok::Constant(<String>),
        VarIdentifier => Tok::VarIdentifier(<String>)
    }
}

pub Filter : Node = {
    <l:@L> <defs:DefinitionBlock*> <first:ContentBlock> <rest:AnyBlock*> <r:@R> => {
        let mut nodes = defs;
        nodes.push(first);
        nodes.extend(rest);
        Node::Filter(Filter {
            nodes: nodes,
            location: AstLocation::new(l, r, file.clone())
        })
    },
    <l:@L> <defs:DefinitionBlock*> <r:@R> => Node::Filter(
        Filter {
            nodes: defs,
            location: AstLocation::new(l, r, file.clone())
        }
    ),
};

DefinitionBlock : Node = {
    VarDefinition,
    ImportBlock
};

ImportBlock : Node = <l:@L> "Import" <path:RawStrLiteral> <r:@R> "\n" => Node::Import(
    ImportStatement {
        path: path,
        location: AstLocation::new(l, r, file.clone())
    }
);

BlockType : BlockType = {
    "Show" => BlockType::Show,
    "Hide" => BlockType::Hide
};

ContentBlock : Node = {
    <l:@L> <t:BlockType> <condition:("if" <ValueExpression>)?> "\n" <stmts:Statement*> <r:@R> => Node::Block(
        Block {
            nodes: stmts,
            variant: t,
            location: AstLocation::new(l, r, file.clone()),
            condition: condition.and_then(|inner| Some(Box::new(inner)))
        }
    ),
    <l:@L> "Mixin" <name:Constant> <args:("(" <Comma<VarIdentifier>> ")")?> "\n" <instructions:Statement*> <r:@R> => {
        let params = args
            .unwrap_or(vec![])
            .iter()
            .map(|param_name| ast::mixin::Param { name: param_name.clone(), default: None })
            .collect();
        Node::Mixin(
            ast::mixin::Mixin{
                name: name,
                parameters: params,
                statements: instructions,
                location: AstLocation::new(l, r, file.clone())
            }
        )
    }
};

AnyBlock : Node = { ImportBlock, ContentBlock };

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

VarDefinition : Node  = {
    <l:@L> <id:VarIdentifier> "=" <v:Value> <r:@R> "\n" => Node::VarDefinition(
        VarDefinition {
            identifier: id,
            values: Box::new(v),
            location: AstLocation::new(l, r, file.clone())
        }
    )
};

VarReference : Node = {
    <l:@L> <ident:VarIdentifier> <r:@R> => Node::Value(ExpressionValue::Var(
        VarReference {
            identifier: ident,
            location: AstLocation::new(l, r, file.clone())
        }
    ))
};

SetValueStmt : Node = {
    <l:@L> <n:Constant> <val:Value> <r:@R> "\n" => Node::SetValueStmt(
        stm::SetValueStatement {
            name: n,
            values: Box::new(val),
            location: AstLocation::new(l, r, file.clone())
        }
    )
};

ConditionStmt : Node = {
    <l:@L> <n: Constant> <cond: StmCondition> <r:@R> "\n" => Node::ConditionStmt(
        stm::ConditionStatement {
            name: n,
            condition: cond,
            location: AstLocation::new(l, r, file.clone())
        }
    )
};

StmCondition : stm::Condition = {
    <op:ComparisonOperator> <v:ValueExpression> => stm::Condition { value: Box::new(v), operator: op }
};

MixinCall : Node = {
    <l:@L> "+" <n:Constant> <params:("(" <Comma<Value>> ")")?> <r:@R> "\n" => Node::MixinCall(
        ast::mixin::MixinCall {
            name: n,
            parameters: params.unwrap_or(vec![]),
            location: AstLocation::new(l, r, file.clone())
        }
    )
};

Statement : Node = {
    SetValueStmt,
    ConditionStmt,
    MixinCall,
    VarDefinition,
};

RawStrLiteral : String = {
    QuotedStrLiteral => String::from(<>),
    Constant => String::from(<>),
};

StrLiteral : Node = {
    <l:@L> <s:RawStrLiteral> <r:@R> => Node::Value(
        ExpressionValue::String(s)
    )
};

ComparisonOperator : stm::ComparisonOperator = {
    ">=" => stm::ComparisonOperator::Gte,
    ">" =>  stm::ComparisonOperator::Gt,
    "<=" => stm::ComparisonOperator::Lte,
    "<" =>  stm::ComparisonOperator::Lt,
    "=" =>  stm::ComparisonOperator::Eql
};

NumberLiteral : Node = {
    <l:@L> <num:Num> <r:@R> => Node::Expression(
        ExpressionNode::Val(ExpressionValue::Int(num), AstLocation::new(l, r, file.clone()))
    ),
    <l:@L> <num:Float> <r:@R> => Node::Expression(
        ExpressionNode::Val(ExpressionValue::Decimal(num), AstLocation::new(l, r, file.clone()))
    ),
};

Bool : Node = {
    <l:@L> "True" <r:@R> => Node::Expression(
        ExpressionNode::Val(ExpressionValue::Bool(true), AstLocation::new(l, r, file.clone()))
    ),
    <l:@L> "False" <r:@R> => Node::Expression(
        ExpressionNode::Val(ExpressionValue::Bool(false), AstLocation::new(l, r, file.clone()))
    )
};

Value : Node = {
    <values:ValueExpression+> => {
        if values.len() == 1 {
            values[0].clone()
        } else {
            Node::Value(ExpressionValue::List(values))
        }
    }
};

ValueExpression : Node = {
    <l:ValueExpression> <op:ComparisonOperator> <r:SumExpression> => Node::Expression(
        ExpressionNode::Op(Box::new(l), ExpressionOperation::from(op), Box::new(r))
    ),
    SumExpression
};

SumExpression : Node = {
    <l:SumExpression> "+" <r:FactorExpression> => Node::Expression(
        ExpressionNode::Op(Box::new(l), ExpressionOperation::Add, Box::new(r))
    ),
    <l:SumExpression> "-" <r:FactorExpression> => Node::Expression(
        ExpressionNode::Op(Box::new(l), ExpressionOperation::Sub, Box::new(r))
    ),
    FactorExpression
};

FactorExpression : Node = {
    <l:FactorExpression> "*" <r:ExpressionTerm> => Node::Expression(
        ExpressionNode::Op(Box::new(l), ExpressionOperation::Mul, Box::new(r))
    ),
    <l:FactorExpression> "/" <r:ExpressionTerm> => Node::Expression(
        ExpressionNode::Op(Box::new(l), ExpressionOperation::Div, Box::new(r))
    ),
    ExpressionTerm
};

ExpressionTerm : Node = {
    VarReference,
    NumberLiteral,
    StrLiteral,
    Bool,
    "(" <ValueExpression> ")" => <>
};

Color: Node = {
    <r:ExpressionTerm> <g:ExpressionTerm> <b:ExpressionTerm> <a:ExpressionTerm> => Node::Color(ast::color::Color {
        r: Box::new(r),
        g: Box::new(g),
        b: Box::new(b),
        a: Box::new(a)
    }),
    <left:@L> <r:ExpressionTerm> <g:ExpressionTerm> <b:ExpressionTerm> <right:@R> => Node::Color(ast::color::Color {
        r: Box::new(r),
        g: Box::new(g),
        b: Box::new(b),
        a: Box::new(Node::Expression(
            ExpressionNode::Val(ExpressionValue::Int(255),
            AstLocation::new(left, right, file.clone())
        )))
    })
};
