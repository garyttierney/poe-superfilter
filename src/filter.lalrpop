
use ast;
use ast::block_statements as stm;
use tok::Location as TokenLocation;
use tok::Tok;

//#[LALR]
grammar;

extern {
    type Location = TokenLocation;
    type Error = char;

    enum Tok {
        "(" => Tok::LParen,
        ")" => Tok::RParen,
        "-" => Tok::Minus,
        "+" => Tok::Plus,
        "*" => Tok::Times,
        "/" => Tok::Div,
        "," => Tok::Comma,
        "\n" => Tok::NewLine,
        "Show" => Tok::Show,
        "Hide" => Tok::Hide,
        "Mixin" => Tok::Mixin,
        "Import" => Tok::Import,
        ">=" => Tok::Gte,
        ">" => Tok::Gt,
        "<=" => Tok::Lte,
        "<" => Tok::Lt,
        "=" => Tok::Eql,
        Num => Tok::Num(<i32>),
        QuotedStrLiteral => Tok::StrLiteral(<String>),
        Constant => Tok::Constant(<String>),
        VarIdentifier => Tok::VarIdentifier(<String>)
    }
}

pub Filter : Box<Vec<ast::Block>> = {
    <defs:DefinitionBlock*> <first:ContentBlock> <rest:AnyBlock*> => {
        let mut v = defs;
        v.push(first);
        v.extend(rest);
        Box::new(v)
    },
    DefinitionBlock* => Box::new(<>),
};

DefinitionBlock : ast::Block = {
    VarDefinition => ast::Block::Var(<>),
    ImportBlock
};

ImportBlock : ast::Block = "Import" <StrLiteral> "\n" => ast::Block::Import(<>);

ContentBlock : ast::Block = {
    "Show" "\n" <Statement*> => ast::Block::Show(<>),
    "Hide" "\n" <Statement*> => ast::Block::Hide(<>),
    "Mixin" <name:Constant> <args:("(" <Comma<VarIdentifier>> ")")?> "\n" <instructions:Statement*> => {
        let params = args
            .unwrap_or(vec![])
            .iter()
            .map(|param_name| ast::mixin::Param { name: param_name.clone(), default: None })
            .collect();
        ast::Block::Mixin(
            ast::mixin::Mixin{
                name: name,
                parameters: params,
                statements: instructions
            }
        )
    }
};

AnyBlock : ast::Block = { ImportBlock, ContentBlock };

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

VarDefinition : ast::VarDefinition  = {
    <id:VarIdentifier> "=" <v:Value+> "\n" => ast::VarDefinition { identifier: id, values: v }
};

SetValueStmt : stm::SetValueStatement = {
    <n:Constant> <val:Value+> "\n" => stm::SetValueStatement { name: n, values: val }
};

ConditionStmt : stm::ConditionStatement = {
    < n: Constant > < cond: Condition > "\n" => stm::ConditionStatement { name: n, condition: cond }
};

MixinCall : ast::mixin::MixinCall = {
    "+" <n:Constant> <params:("(" <Comma<Value>> ")")?> "\n" => ast::mixin::MixinCall { name: n, parameters: params.unwrap_or(vec![]) }
};

Statement : stm::Statement = {
    SetValueStmt => stm::Statement::SetValue(<>),
    ConditionStmt => stm::Statement::Condition(<>),
    VarDefinition => stm::Statement::Var(<>),
    MixinCall => stm::Statement::Include(<>)
};

Value : ast::Value = {
    NumExpression => ast::Value::Num(<>),
    StringExpression => ast::Value::Str(<>),
    VarIdentifier => ast::Value::Var(<>)
};

StringExpression : ast::strings::StringBox = {
    StrLiteral => ast::strings::StringBox::Value(<>),
};

StrLiteral : String = {
    QuotedStrLiteral => String::from(<>),
    Constant => String::from(<>),
};

Condition : stm::Condition = {
    <op:ComparisonOperator> <v:Value> => stm::Condition { value: v, operator: op }
};

ComparisonOperator : stm::ComparisonOperator = {
    ">=" => stm::ComparisonOperator::Gte,
    ">" => stm::ComparisonOperator::Gt,
    "<=" => stm::ComparisonOperator::Lte,
    "<" => stm::ComparisonOperator::Lt,
    "=" => stm::ComparisonOperator::Eql
};

NumExpression : ast::numbers::NumberExpression = {
    <l:NumExpression> "+" <r:NumFactor> => ast::numbers::NumberExpression::Op(Box::new(l), ast::numbers::NumberOperation::Add, Box::new(r)),
    <l:NumExpression> "-" <r:NumFactor> => ast::numbers::NumberExpression::Op(Box::new(l), ast::numbers::NumberOperation::Sub, Box::new(r)),
    NumFactor
};

NumFactor : ast::numbers::NumberExpression = {
    <l:NumFactor> "*" <r:NumTerm> => ast::numbers::NumberExpression::Op(Box::new(l), ast::numbers::NumberOperation::Mul, Box::new(r)),
    <l:NumFactor> "/" <r:NumTerm> => ast::numbers::NumberExpression::Op(Box::new(l), ast::numbers::NumberOperation::Div, Box::new(r)),
    NumTerm
};

NumTerm : ast::numbers::NumberExpression = {
    Num => ast::numbers::NumberExpression::Number(ast::numbers::NumberBox::IntValue(<>)),
    "(" <NumExpression> ")" => <>
};

Color: ast::Color = {
    <r:NumExpression> <g:NumExpression> <b:NumExpression> <a:NumExpression> => ast::Color {
        r: r,
        g: g,
        b: b,
        a: a
    },
    <r:NumExpression> <g:NumExpression> <b:NumExpression> => ast::Color {
        r: r,
        g: g,
        b: b,
        a: ast::numbers::NumberExpression::Number(ast::numbers::NumberBox::IntValue(255))
    }
};
